file(
        GLOB_RECURSE
        MDFlex_SRC
        "src/*.cpp"
        "src/*.h"
)

add_executable(md-flexible ${MDFlex_SRC}
        src/templateInstantiations/iteratePairwiseLJFunctorAbsoluteSites.cpp
        src/configuration/objects/AbsoluteMultiSiteMoleculeInitializer.cpp
        ../../applicationLibrary/molecularDynamics/molecularDynamicsLibrary/Site.h
        ../../applicationLibrary/molecularDynamics/molecularDynamicsLibrary/Site.cpp
        src/temporaryTests.h
        src/temporaryTests.cpp
)

target_include_directories(md-flexible PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})

include(autopas_yaml-cpp)
include(autopas_all)

option(MD_FLEXIBLE_USE_MPI "Enable MPI parallelism for md-flexible." OFF)

target_link_libraries(md-flexible PUBLIC autopas autopasTools molecularDynamicsLibrary yaml-cpp  ${ALL_LIB} $<$<BOOL:${MD_FLEXIBLE_USE_MPI}>:MPI::MPI_CXX>)

option(I_JUST_WANNA_RUN_UNIT_TESTS " " ON)
option(MD_FLEXIBLE_USE_BUNDLING_MULTISITE_APPROACH "Defines whether or not a multisite-simulation should be run using Single-Site Particles bundled to Molecules or not" ON)
option(MD_FLEXIBLE_FUNCTOR_AUTOVEC "Compile AutoVec Functor without calculation of global values." OFF)
option(MD_FLEXIBLE_FUNCTOR_AUTOVEC_GLOBALS "Compile AutoVec Functor with calculation of global values." OFF)
option(MD_FLEXIBLE_FUNCTOR_ABSOLUTE_POS "Compile AutoVec Functor using absolute site positions." OFF)
option(MD_FLEXIBLE_ABSOLUTE_POS_USE_FLATTENING "Temporary option- If MD_FLEXIBLE_FUNCTOR_ABSOLUTE_POS is set this variable defines whether or not it should flatten the vectors before using them." OFF)# ON)
option(MD_FLEXIBLE_FUNCTOR_AVX "If instruction set is available, compile AVX Functor without calculation of global values." OFF)
option(MD_FLEXIBLE_FUNCTOR_SVE "If instruction set is available, compile SVE Functor without calculation of global values." OFF)
option(MD_FLEXIBLE_TORQUE_AFTER_FORCE "If set the torque calculation doesn't get managed by the force functor but by time discretization drastically reducing the number floating point operations required" OFF)

target_compile_definitions(
        md-flexible
        PUBLIC
        $<$<BOOL:${I_JUST_WANNA_RUN_UNIT_TESTS}>:I_JUST_WANNA_RUN_UNIT_TESTS>
        $<$<BOOL:${MD_FLEXIBLE_USE_BUNDLING_MULTISITE_APPROACH}>:MD_FLEXIBLE_USE_BUNDLING_MULTISITE_APPROACH>
        $<$<BOOL:${MD_FLEXIBLE_FUNCTOR_AUTOVEC}>:MD_FLEXIBLE_FUNCTOR_AUTOVEC>
        $<$<BOOL:${MD_FLEXIBLE_FUNCTOR_ABSOLUTE_POS}>:MD_FLEXIBLE_FUNCTOR_ABSOLUTE_POS>
        $<$<BOOL:${MD_FLEXIBLE_ABSOLUTE_POS_USE_FLATTENING}>:MD_FLEXIBLE_ABSOLUTE_POS_USE_FLATTENING>
        $<$<BOOL:${MD_FLEXIBLE_FUNCTOR_AUTOVEC_GLOBALS}>:MD_FLEXIBLE_FUNCTOR_AUTOVEC_GLOBALS>
        $<$<BOOL:${MD_FLEXIBLE_FUNCTOR_AVX}>:MD_FLEXIBLE_FUNCTOR_AVX>
        $<$<BOOL:${MD_FLEXIBLE_FUNCTOR_SVE}>:MD_FLEXIBLE_FUNCTOR_SVE>
        $<$<BOOL:${MD_FLEXIBLE_TORQUE_AFTER_FORCE}>:MD_FLEXIBLE_TORQUE_AFTER_FORCE>
)

# --- display warnings if single-/multi-site mode is used with a functor type that has not been implemented ---
string(TOLOWER "${MD_FLEXIBLE_MODE}" MD_FLEXIBLE_MODE_lower)
if (MD_FLEXIBLE_MODE_lower MATCHES "multisite")
    if (MD_FLEXIBLE_FUNCTOR_AVX)
        message(WARNING "AVX Lennard-Jones functor has not been implemented for Multi-Site Molecules")
    endif()
    if (MD_FLEXIBLE_FUNCTOR_SVE)
        message(WARNING "SVE Lennard-Jones functor has not been implemented for Multi-Site Molecules")
    endif()
endif()

# --- copy script files to build dir ---
file(
        GLOB_RECURSE SCRIPTS
        RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
        "*.sh"
        "*.gp"
        "*.py"
        "*.yaml"
        "*.rule"
)

foreach (script ${SCRIPTS})
    configure_file(${CMAKE_CURRENT_SOURCE_DIR}/${script} ${CMAKE_CURRENT_BINARY_DIR} COPYONLY)
endforeach (script)

# -----------------test-----------------

add_subdirectory(tests)

# add check for current target
# cmake-format: off

# randomly generated imbalanced scenario tested with LC only
# also tests the flop functor
add_test(
        NAME md-flexible.test-static
        COMMAND
        md-flexible
        --container linked
        --cutoff 1.
        --distribution-mean 5.0
        --distribution-stddeviation 2.0
        --data-layout soa
        --functor "Lennard-Jones"
        --iterations 10
        --particle-generator gauss
        --particles-total 10
        --traversal c08,sliced
        --verlet-rebuild-frequency 5
        --verlet-skin-radius-per-timestep 0
        --boundary-type none
        --deltaT 0.
        --no-end-config
        --no-progress-bar
        CONFIGURATIONS checkExamples
)


# The AVX example should only be tested if AVX is available.
set(HAS_AVX true)
if (NOT AUTOPAS_USE_VECTORIZATION)
    # If no vectorization is used, don't enable the example.
    message(STATUS "No vectorization used, not adding md-flexible-avx.test-unaligned to ctest.")
    set(HAS_AVX false)
elseif (AUTOPAS_VECTOR_INSTRUCTIONS MATCHES "SSE")
    # If vectorization is set to SSE: don't enable the example
    message(STATUS "Only SSE specified, not adding md-flexible-avx.test-unaligned to ctest.")
    set(HAS_AVX false)
elseif (AUTOPAS_VECTOR_INSTRUCTIONS MATCHES "NATIVE")
    # If Vectorization is set to native: we try to figure out the vectorization level.
    message(STATUS "Native vectorization level, trying to detect AVX capability.")
    try_compile(
            HAS_AVX
            ${CMAKE_BINARY_DIR}
            ${AUTOPAS_SOURCE_DIR}/cmake/tests/has_avx_test.cpp
            # We are abusing COMPILE_DEFINITIONS here. There is no other sane way of passing "-march=native" ...
            COMPILE_DEFINITIONS "-march=native"
            OUTPUT_VARIABLE HAS_AVX_ERROR
    )
else ()
    message(STATUS "Adding AVX example to test, as proper vectorization is manually specified.")
endif ()

if (HAS_AVX)
    set(TEST_FUNCTOR "Lennard-Jones-AVX")
    message(STATUS "AVX detected. Adding AVX test.")
    #dangerous, as lc_c04_combined_SoA uses unaligned values.
    add_test(
            NAME md-flexible-avx.test-unaligned
            COMMAND
            md-flexible
            --no-end-config
            --no-flops
            --functor "${TEST_FUNCTOR}"
            --deltaT 0
            --particle-generator uniform
            --log-level debug
            --traversal lc_c04_combined_SoA
            --particles-total 71
            --no-progress-bar
            CONFIGURATIONS checkExamples
    )
else ()
  message(STATUS "AVX could not be detected. Not testing the AVX Functor.")
  set(TEST_FUNCTOR "Lennard-Jones")
endif ()

#stable, periodic particle grid tested with all configurations
add_test(
        NAME md-flexible.test-sim
        COMMAND
        md-flexible
        --container all
        --traversal all
        --cutoff 1.5
        --functor "${TEST_FUNCTOR}"
        --tuning-phases 1
        --particle-generator grid
        --particles-per-dimension 10
        --particle-spacing 1.1225
        --verlet-rebuild-frequency 4
        --verlet-skin-radius-per-timestep 0.05
        --boundary-type periodic
        --deltaT 0.005
        --no-end-config
        --no-flops
        --no-progress-bar
        CONFIGURATIONS checkExamples
)

add_test(
        NAME md-flexMeasurePerf
        COMMAND measurePerf.sh md-flexible -silent
        CONFIGURATIONS checkExamples
)
# cmake-format: on

# add the executable to checkExamples as dependency
add_dependencies(checkExamples md-flexible)
